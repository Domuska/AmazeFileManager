buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.2'
    }
}
apply plugin: 'com.android.application'

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://jitpack.io" }
}

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.3'

    packagingOptions {
        exclude 'proguard-project.txt'
        exclude 'project.properties'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/DEPENDENCIES'
    }

    defaultConfig {
        applicationId "com.amaze.filemanager"
        minSdkVersion 18
        targetSdkVersion 23
        versionCode 41
        versionName "3.1.0"

        //for GUI tests
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release
        config
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }
    lintOptions {
        abortOnError false
    }

    sourceSets {
        main {
            /*manifest.srcFile 'src/main/AndroidManifest.xml'
            java.srcDirs = ['src', 'src/main/java', 'src/main/java/']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res', 'src/main/res', 'src/main/res/']*/
            assets.srcDirs = ['assets']
        }

        // Move the tests to tests/java, tests/res, etc...
        instrumentTest.setRoot('tests')

        // Move the build types to build-types/<type>
        // For instance, build-types/debug/java, build-types/debug/AndroidManifest.xml, ...
        // This moves them out of them default location under src/<type>/... which would
        // conflict with src/ being used by the main source set.
        // Adding new build types or product flavors should be accompanied
        // by a similar customization.
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }

    productFlavors {
        fdroid {
            buildConfigField "boolean", "IS_VERSION_FDROID", "true"
        }
        play {
            buildConfigField "boolean", "IS_VERSION_FDROID", "false"
            signingConfig signingConfigs.config
        }
    }

}

//for appium, so Gradle does not swallow stdout prints
//http://stackoverflow.com/questions/3963708/gradle-how-to-display-test-results-in-the-console-in-real-time
tasks.withType(Test) {
    testLogging {
        events "started", "passed", "skipped", "failed"
   }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.readystatesoftware.systembartint:systembartint:1.0.3'
    compile 'com.android.support:design:23.2.1'
    compile 'com.github.junrar:junrar:0.7'
    compile 'org.apache.commons:commons-compress:1.10'
    compile 'com.android.support:appcompat-v7:23.2.1'
    compile 'com.android.support:cardview-v7:23.2.1'
    // Google Play Services
    playCompile 'com.google.android.gms:play-services-plus:7.8.0'
    compile 'org.sufficientlysecure:donations:2.3'
    playCompile 'com.nostra13.universalimageloader:universal-image-loader:1.9.4'
    compile project(':libs:StickyHeadersRecyclerView')
    compile 'com.github.clans:fab:1.6.1'
    compile('com.github.afollestad.material-dialogs:core:0.8.5.5@aar') {
        transitive = true
    }
    //FTP Server

    //        compile 'org.apache.mina:mina-core:2.0.13'

    //    compile 'org.apache.ftpserver:ftpserver-core:1.0.6'

    //    compile 'org.apache.ftpserver:ftplet-api:1.0.6'

    // https://mvnrepository.com/artifact/org.apache.mina/mina-core
    compile 'org.apache.mina:mina-core:2.0.13'
    // https://mvnrepository.com/artifact/org.slf4j/slf4j-api
    compile 'org.slf4j:slf4j-api:1.7.21'
    // https://mvnrepository.com/artifact/org.slf4j/slf4j-simple
    compile 'org.slf4j:slf4j-simple:1.7.21'
    compile files('libs/ftplet-api-1.1.0-SNAPSHOT.jar')
    compile files('libs/ftpserver-core-1.1.0-SNAPSHOT.jar')
    //for Appium
    testCompile 'junit:junit:4.12'
    compile files('libs/Appium/apache-mime4j-0.6.jar')
    compile files('libs/Appium/commons-codec-1.10.jar')
    compile files('libs/Appium/commons-collections-3.2.1.jar')
    compile files('libs/Appium/commons-exec-1.3.jar')
    compile files('libs/Appium/commons-io-2.4.jar')
    compile files('libs/Appium/commons-lang3-3.4.jar')
    compile files('libs/Appium/commons-logging-1.1.3.jar')
    compile files('libs/Appium/commons-validator-1.4.1.jar')
    compile files('libs/Appium/gson-2.3.1.jar')
    compile files('libs/Appium/guava-18.0.jar')
    compile files('libs/Appium/hamcrest-core-1.3.jar')
    compile files('libs/Appium/httpclient-4.4.1.jar')
    compile files('libs/Appium/httpcore-4.4.1.jar')
    compile files('libs/Appium/httpmime-4.4.1.jar')
    compile files('libs/Appium/java-client-4.0.0.jar')
    compile files('libs/Appium/selenium-java-2.47.1.jar')

    //for the grantPermissions task
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.4'
    androidTestCompile 'com.android.support:support-annotations:23.2.1'
    androidTestCompile 'com.android.support.test.uiautomator:uiautomator-v18:2.1.2'
}



def Properties props = new Properties()
def propFile = new File('signing.properties')

if (propFile.canRead()) {
    props.load(new FileInputStream(propFile))

    if (props != null &&
            props.containsKey('STORE_FILE') &&
            props.containsKey('STORE_PASSWORD') &&
            props.containsKey('KEY_ALIAS') &&
            props.containsKey('KEY_PASSWORD')) {
        android.signingConfigs.release.storeFile = file(props['STORE_FILE'])
        android.signingConfigs.release.storePassword = props['STORE_PASSWORD']
        android.signingConfigs.release.keyAlias = props['KEY_ALIAS']
        android.signingConfigs.release.keyPassword = props['KEY_PASSWORD']
    } else {
        println 'signing.properties found but some entries are missing'
        android.buildTypes.release.signingConfig = null
    }
} else {
    println 'signing.properties not found'
    android.buildTypes.release.signingConfig = null
}
